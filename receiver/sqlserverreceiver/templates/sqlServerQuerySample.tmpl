SELECT TOP(@top)
  DB_NAME(r.database_id) AS db_name,
  ISNULL(c.client_net_address, '') as client_address,
  ISNULL(c.client_tcp_port, '') AS client_port,
  CONVERT(NVARCHAR, TODATETIMEOFFSET(r.start_time, DATEPART(TZOFFSET, SYSDATETIMEOFFSET())), 126) AS query_start,
  s.session_id,
  s.STATUS AS session_status,
  r.STATUS AS request_status,
  ISNULL(s.host_name, '') AS host_name,
  r.command,
  SUBSTRING(o.TEXT, (r.statement_start_offset / 2) + 1, (
      (
        CASE r.statement_end_offset
          WHEN - 1
            THEN DATALENGTH(o.TEXT)
          ELSE r.statement_end_offset
          END - r.statement_start_offset
        ) / 2
      ) + 1) AS statement_text,
  r.blocking_session_id,
  ISNULL(r.wait_type, '') AS wait_type,
  r.wait_time,
  r.wait_resource,
  r.open_transaction_count,
  r.transaction_id,
  r.percent_complete,
  r.estimated_completion_time,
  r.cpu_time,
  r.total_elapsed_time,
  r.reads,
  r.writes,
  r.logical_reads,
  r.transaction_isolation_level,
  r.lock_timeout,
  r.deadlock_priority,
  r.row_count,
  ISNULL(r.query_hash, CONVERT(VARBINARY, '')) AS query_hash,
  ISNULL(r.query_plan_hash, CONVERT(VARBINARY, '')) AS query_plan_hash,
  ISNULL(r.context_info, CONVERT(VARBINARY, '')) AS context_info,
  s.login_name AS username
FROM sys.dm_exec_requests r
INNER JOIN sys.dm_exec_sessions s ON r.session_id = s.session_id
INNER JOIN sys.dm_exec_connections c ON s.session_id = c.session_id
CROSS APPLY sys.dm_exec_sql_text(r.plan_handle) AS o;